# 20190910

## Minimal ECS setting with terraform

```terraform

variable "aws_account_id" {}

provider "aws" {
  profile = "default"
  region  = "ap-northeast-1"
}

resource "aws_ecs_cluster" "ecs_cluster" {
  name = "sample-app-20190910"
}

resource "aws_ecs_service" "ecs-service" {
  name            = "ecs-service"
  cluster         = "${aws_ecs_cluster.ecs_cluster.id}"
  task_definition = "${aws_ecs_task_definition.task_definition.arn}"
  desired_count   = 1
  launch_type     = "FARGATE"

  network_configuration {
    subnets          = ["${aws_subnet.private-1.id}", "${aws_subnet.private-2.id}"]
    security_groups  = ["${aws_security_group.sg.id}"]
    assign_public_ip = "true"
  }
}

resource "aws_ecs_task_definition" "task_definition" {
  family                   = "ecs-task-definition"
  network_mode             = "awsvpc"
  requires_compatibilities = ["FARGATE"]
  execution_role_arn       = "arn:aws:iam::${var.aws_account_id}:role/ecsTaskExecutionRole"
  cpu                      = 256
  memory                   = 512
  container_definitions    = <<EOL
[
  {
    "name"         : "sample",
    "image"        : "nginx:latest",
    "network_mode" : "awsvpc",
    "requiresCompatibilities": [
      "FARGATE"
    ], 
    "cpu"          : 256,
    "memory"       : 512,
    "portMappings": [{
      "containerPort": 80,
      "hostPort"     : 80
    }]
  }
]
EOL
}

resource "aws_vpc" "main" {
  cidr_block = "10.0.0.0/16"
}

resource "aws_security_group" "sg" {
  name   = "security_group for ${aws_ecs_cluster.ecs_cluster.name}"
  vpc_id = "${aws_vpc.main.id}"

  ingress {
    protocol    = "tcp"
    from_port   = 80
    to_port     = 80
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

resource "aws_internet_gateway" "ig" {
  vpc_id = "${aws_vpc.main.id}"
}

resource "aws_route_table" "rt" {
  vpc_id = "${aws_vpc.main.id}"
  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = "${aws_internet_gateway.ig.id}"
  }
}

resource "aws_subnet" "private-1" {
  cidr_block = "10.0.0.0/24"
  vpc_id     = "${aws_vpc.main.id}"
}

resource "aws_subnet" "private-2" {
  cidr_block = "10.0.1.0/24"
  vpc_id     = "${aws_vpc.main.id}"
}

resource "aws_route_table_association" "vpc-rta-1" {
    subnet_id      = "${aws_subnet.private-1.id}"
    route_table_id = "${aws_route_table.rt.id}"
}

resource "aws_route_table_association" "vpc-rta-2" {
    subnet_id      = "${aws_subnet.private-2.id}"
    route_table_id = "${aws_route_table.rt.id}"
}

```
